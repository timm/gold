#- vim: ft=awk ts=2 sw=2 et :

------------------------------------------
general support code.
 
copyright (c) 2020, tim menzies.   
licensed under the mit license 
for full license info, see license.md in the project root

------------------------------------------

## Globals
There is only one.

BEGIN { List(Gold); 
  Gold.test.epsilon=0.00001
  Gold.pi= 3.141592653
  Gold.e = 2.718281828
  Gold.dot=sprintf("%c",46)  }

------------------------------------------

##  Object stuff 
Methods for handling objects.

function List(i:untyped) { 
  ## Initialize an empty list
  split("",i,"") }

function Object(i:untyped) { 
  ## Initialize a new object, give it a unique id (in `i.id`)
  List(i); i.id = ++Gold.id }

function has(i:untyped, k:atom, f:?fname) { 
  ## Create something of class `f` inside of `i` at position `k`
  f=f?f:"List";i[k][0]; @f(i[k]); delete i[k][0]; return k}

function haS(i:untyped, k:atom, f:?fname, x:any)  { 
  ## Like `has`, but accepts one constructor argument `x`.
  i[k][0]; @f(i[k],x); delete i[k][0] }

function hAS(i:untyped, k:atom, f:fname, x:any, y:any) { 
  ## Like `has`, but accepts two constructor arguments `x` and `y`..
  i[k][0]; @f(i[k],x,y); delete i[k][0] }

function new(i:array, f:fname, k) { 
  ## Add a new instances of class `f` at the end of `i`.
  k=length(i)+1; has(i,k,f); return k }

function asNum(x:atom,    y) {
  ## If `x` can be coerced number, return that. Else return `x`.
  y = x+0
  return x==y ? x : y }

------------------------------------------

## Testing stuff 
Support for unit testing.

function rogues(   s,ignore) { 
  ## Print local variables, escaped from functions
  ignore=       "(ARGV|ROUNDMODE|ORS|OFS|LINT|FNR"
  ignore=ignore "|ERRNO|NR|IGNORECASE|TEXTDOMAIN|NF|ARGIND"
  ignore=ignore "|ARGC|PROCINFO|FIELDWIDTHS|CONVFMT|SUBSEP"
  ignore=ignore "|PREC|ENVIRON|RS|FPAT|RT|RLENGTH|OFMT"
  ignore=ignore "|FS|RSTART|FILENAME|BINMODE)"
  for(s in SYMTAB) 
    if (s ~ /^[A-Z]/)  
      if (s !~ ignore)
         print "#W> Global: " s>"/dev/stderr" 
  for(s in SYMTAB) 
    if (s ~ /^[_a-z]/) 
      print "#W> Rogue: "  s>"/dev/stderr" }

function tests(s:string,     a,i,f) {
  ## Run the functions names in the comma-separated string `s`.
  split(s,a,",") 
  for(i in a) {
     f=a[i]
     if(f in FUNCTAB)
       @f(s) }}

function ok(s:string, got:atom, want:atom,   epsilon:number,       good) {
  ## Print "PASS" if `got` same as `want1` (and print "FAIL" otherwise).
  epsilon = epsilon ? epsilon : Gold.test.epsilon
  if (typeof(want) == "number") 
    good = abs(want - got)/(want + 10^-32)  < epsilon
  else
    good = want == got;
  s= "#TEST:\t"(good?"PASSED":"FAILED") "\t" want "\t" got " : " s
  rint(good ? green(s) : red(s)) }

function red(x)   { return "\033[31m"x"\033[0m" }
function green(x) { return "\033[32m"x"\033[0m" }

------------------------------------------

## Array stuff 
Support for managing arrays.

function push(a:array, x:atom) { 
  ## Return `x` after adding to the end of `a`.
  a[length(a)+1] = x; return x }

function keysort(a,k) { 
  ## Sort `a` by field `k`.
  Gold["keysort"]=k; return asort(a,a,"keysort1") }

function keysort1(i1,x,i2,y) {
  return sortCompare(x[ Gold["keysort"] ] + 0,
                     y[ Gold["keysort"] ] + 0) } 

function sortCompare(x,y) { return x<y ? -1 : (x==y?0:1) }

function copy(a:array,b:array,   j) { 
  ## `b` is set to a recursively copy of `a`.
  for(j in a) 
    if(isarray(a[j]))  {
      b[j][0]
      delete b[j][0]
      copy(a[j], b[j]) 
    } else
      b[j] = a[j] 
}

------------------------------------------

## Maths stuff 
Some mathematical details.

function abs(x:number)           { 
  ## Return absolute value of `x`.
  return x>0 ? x : -1*x }

function max(x:number, y:number) { 
  ## Return max of `x` and `y.
  return x>y ? x : y }

function min(x:number, y:number) { 
  ## Return min of `x` and `y.
  return x>y ? y : x }

function z(mu:nummber|0, sd:number|0) {
  ## Sample from a Gaussian.
  mu = mu?mu:0
  sd = sd?sd:1  
  return mu + sd*sqrt(-2*log(rand()))*cos(2*Gold.pi*rand())}

------------------------------------------

### Printing stuff 

function o(a:array, prefix:string|"",     i,sep,s) {
  ## Simple printing of a flat array
  for(i in a) {s = s sep prefix a[i]; sep=","}
  return  s }

function oo(a,prefix,    indent,   i,txt) {
  txt = indent ? indent : (prefix Gold.dot )
  if (!isarray(a)) {print(a); return a}
  ooSortOrder(a)
  for(i in a)  {
    if (isarray(a[i]))   {
      print(txt i"" )
      oo(a[i],"","|  " indent)
    } else
      print(txt i (a[i]==""?"": ": " a[i])) }}

function ooSortOrder(a, i) {
  for (i in a)
   return PROCINFO["sorted_in"] =\
     typeof(i+1)=="number" ? "@ind_num_asc" : "@ind_str_asc" }


------------------------------------------

## File stuff 

function csv(a:array, f:string|"",       b4, g,txt,i,old,new) {
  ## Loop over a csv file `f`, setting the array `a` to the next record.
  ## Returns zero at end of files.
  ## Complain if file is missing. Kill comments `#` and spaces.
  ## Split lines on commas. If records split over more than one 
  ## line (and end in a comma) concat that line to the next.
  ## Coerce numeric strings to numbers.
  f = f ? f : "-"             
  g = getline < f
  if (g< 0) { print "#E> Missing f ["f"]"; exit 1 } # file missing
  if (g==0) { close(f) ; return 0 }       # end of file                   
  txt = b4 $0                             # combine with prior
  gsub(/[ \t]+/,"",txt)
  if (txt ~ /,$/) { return csv(a,f,txt) } # continue txt into next
  sub(/#.*/, "", txt)                    # kill whitespace,comments    
  if (!txt)       { return csv(a,f,txt) } # skip blanks
  split(txt, a, ",")                      # split on "," into "a"
  for(i in a) {
     old = a[i]
     new = a[i]+0
     a[i] = (old == new) ? new : old
  }
  return 1 } 
